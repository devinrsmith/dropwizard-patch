/*
 * Copyright 2014 Tariq Bugrara
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

version '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile(
            'io.dropwizard:dropwizard-core:0.7.1',
            'io.dropwizard:dropwizard-testing:0.7.1'
    )
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

test {
    filter {
        includeTestsMatching "io.progix.dropwizard.patch.test.*"
    }
}

signing {
    sign configurations.archives
    sign sourceJar
    sign jar
    sign javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'io.progix.dropwizard'
            artifactId 'dropwizard-patch'
            version '0.1.0'

            from components.java

            artifact sourceJar
            artifact javadocJar

            pom.withXml {
                asNode().appendNode('name', 'Dropwizard Patch')
                asNode().appendNode('description',
                        'An implementation of the HTTP method PATCH using RFC6902 for the Dropwizard framework.')
                asNode().appendNode('url', 'https://github.com/tbugrara/dropwizard-patch')

                def license = asNode().appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')

                def developer = asNode().appendNode('developers').appendNode('developer')
                developer.appendNode('name', 'Tariq Bugrara')
                developer.appendNode('email', 'tbugrara@gmail.com')
                developer.appendNode('url', 'https://github.com/tbugrara/dropwizard-patch')

                def scm = asNode().appendNode('scm');
                scm.appendNode('connection', 'scm:git:git@github.com:tbugrara/dropwizard-patch.git')
                scm.appendNode('developerConnection', 'scm:git:git@github.com:tbugrara/dropwizard-patch.git')
                scm.appendNode('url', 'git@github.com:tbugrara/dropwizard-patch.git')
            }
        }
    }
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            url "file:///Users/tariqbugrara/dev/repo/maven"
        }
    }
}